buildscript {
    repositories {
        google {
            content {
                includeGroupByRegex "com\\.android.*"
                includeGroupByRegex "androidx.*"
                includeGroupByRegex "android.arch.*"
                includeGroupByRegex "com\\.google.*"
            }
        }
        if (project.hasProperty("useMavenLocal")) {
            mavenLocal()
        }
    }
    dependencies {
        if (project.hasProperty("useMavenLocal")) {
            classpath "com.project.starter.local:easylauncher:+"
        } else {
            classpath "com.project.starter:easylauncher"
        }
    }
}

plugins {
    id 'com.starter.config' version "0.15.0"
}

commonConfig {
    javaFilesAllowed false
    versioningPlugin {
        enabled false
    }
}

subprojects {

    pluginManager.withPlugin("com.android.application") {
        android.applicationVariants.configureEach { variant ->
            variant.mergedFlavor.applicationIdSuffix = variant.name
            variant.mergedFlavor.manifestPlaceholders = [appName: "a-$variant.flavorName-$variant.buildType.name"]
        }

        tasks.register("installAll") {
            android.applicationVariants.configureEach { variant ->
                dependsOn("install${variant.name.capitalize()}")
            }
        }
    }
}
