apply plugin: 'com.starter.application.android'
apply plugin: 'com.starter.easylauncher'

android {

    defaultConfig {
        minSdkVersion 15
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug

            //Debuggable, will get a default ribbon in the launcher icon
        }
        beta {
            signingConfig signingConfigs.debug

            //Debuggable, will get a default ribbon in the launcher icon
            debuggable true
        }
        canary {
            signingConfig signingConfigs.debug

            //Non-debuggable, will not get any default ribbon
            debuggable false
        }
        release {
            signingConfig signingConfigs.debug

            //Non-debuggable, will not get any default ribbon
        }
    }

    flavorDimensions 'environment'
    productFlavors {
        local {
            dimension 'environment'
            applicationIdSuffix = ".local"
            versionNameSuffix ".local"
        }
        qa {
            dimension 'environment'
            applicationIdSuffix = ".qa"
            versionNameSuffix ".qa"
        }
        staging {
            dimension 'environment'
            applicationIdSuffix = ".staging"
            versionNameSuffix ".staging"
        }
        production {
            dimension 'environment'
        }
    }
}

easylauncher {
    defaultFlavorNaming = true // Use flavor name for default ribbon, instead of the type name

    productFlavors {
        local {}
        qa {
            // Add one more filter to all `qa` variants
            filters = redRibbonFilter()
        }
        staging {
            filters = [
                    customColorRibbonFilter("custom", "#556600CC", "#FFFFFF", "bottom", 0.2)
            ]
        }
        production {}
    }

    buildTypes {
        beta {
            // Add two more filters to all `beta` variants
            filters = [
                    customColorRibbonFilter("#0000FF"),
                    overlayFilter(new File("example-custom/launcherOverlay/beta.png"))
            ]
        }
        canary {
            // Remove ALL filters to `canary` variants
            enable false
        }
        release {}
    }

    variants {
        productionDebug {
            // OVERRIDE all previous filters defined for `productionDebug` variant
            filters = orangeRibbonFilter("custom")
        }
    }
}

dependencies {
    implementation "com.google.android.material:material:1.3.0-alpha02"
}
